__author__ = "Jessika Nordin"
__copyright__ = "Copyright 2021, Jessika Nordin"
__email__ = "jessika.nordin@scilifelab.uu.se"
__license__ = "GPL-3"


include: "rules/common.smk"


report: "report/workflow.rst"


#include: "rules/vcf_filter.smk"


rule all:
    input:
        ["vcf_final/%s.vcf.gz" % sample for sample in get_samples(samples)],
        ["parabricks/deepvariant/%s.vcf" % sample for sample in get_samples(samples)],
        "qc/multiqc/MultiQC.html",

#        ["vcf_filter/%s.vcf.gz" % sample for sample in get_samples(samples)],
# can change the function in common-smk to use : unpack(compile_output_list),


module prealignment:
    snakefile:
        github("hydra-genetics/prealignment", path="workflow/Snakefile", tag=config["modules"]["prealignment"],)
    config:
        config


use rule * from prealignment as module_*


module parabricks:
    snakefile:
        github("hydra-genetics/parabricks", path="workflow/Snakefile", tag=config["modules"]["parabricks"],)
    config:
        config


use rule fq2bam from parabricks as parabricks_fq2bam with:
    output:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
        bai="parabricks/fq2bam/{sample}_{type}.bam.bai",
        metrics="parabricks/fq2bam/{sample}_{type}.metrics",
        recal="parabricks/fq2bam/{sample}_{type}.txt",


use rule deepvariant from parabricks as parabricks_deepvariant


include: "rules/add_ref_to_vcf.smk"


module qc:
    snakefile:
        github("hydra-genetics/qc", path="workflow/Snakefile", tag=config["modules"]["qc"],)
    config:
        config

use rule multiqc from qc as qc_multiqc with:
    output:
        "qc/multiqc/MultiQC.html",
        directory("qc/multiqc/MultiQC_data"),

use rule fastqc from qc as qc_fastqc with:
    output:
        html="qc/fastqc/{sample}_{type}_{read}_fastqc.html",
        zip="qc/fastqc/{sample}_{type}_{read}_fastqc.zip",
        tmp="qc/fastqc/{sample}_{type}_{read}/tmp.txt",

use rule picard_collect_duplication_metrics from qc as qc_pic_dup_met with:
    input:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
    output:
        metrics="qc/picard_collect_duplication_metrics/{sample}_{type}.duplication_metrics.txt",

use rule picard_collect_insert_size_metrics from qc as qc_pic_ins_size with:
    input:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
    output:
        txt="qc/picard_collect_insert_size_metrics/{sample}_{type}.insert_size_metrics.txt",
        pdf="qc/picard_collect_insert_size_metrics/{sample}_{type}.insert_size_histogram.pdf",

use rule picard_collect_gc_bias_metrics from qc as qc_pic_gc_met with:
    input:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],
    output:
        chart="qc/picard_collect_gc_bias_metrics/{sample}_{type}.gc_bias.pdf",
        metrics="qc/picard_collect_gc_bias_metrics/{sample}_{type}.gc_bias.detail_metrics",
        summary="qc/picard_collect_gc_bias_metrics/{sample}_{type}.gc_bias.summary_metrics",

use rule picard_collect_multiple_metrics from qc as qc_pic_multi_met with:
    input:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],

use rule picard_collect_alignment_summary_metrics from qc as qc_pic_align_sum_met with:
    input:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],
    output:
        metrics="qc/picard_collect_alignment_summary_metrics/{sample}_{type}.alignment_summary_metrics.txt",

use rule picard_collect_hs_metrics from qc as qc_pic_hs_met with:
    input:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
        bait_intervals=config["reference"]["design_intervals"],
        target_intervals=config["reference"]["design_intervals"],
        reference=config["reference"]["fasta"],
    output:
        "qc/picard_collect_hs_metrics/{sample}_{type}.HsMetrics.txt",


use rule samtools_stats from qc as qc_samtools_stats with:
    input:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
    output:
        "qc/samtools_stats/{sample}_{type}.samtools-stats.txt"

use rule samtools_idxstats from qc as qc_samtools_idxstats with:
    input:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
        bai="parabricks/fq2bam/{sample}_{type}.bam.bai",
    output:
        "qc/samtools_idxstats/{sample}_{type}.samtools-idxstats.txt"

use rule mosdepth from qc as qc_mosdepth with:
    input:
        bam="parabricks/fq2bam/{sample}_{type}.bam",
        bai="parabricks/fq2bam/{sample}_{type}.bam.bai",
        bed=config["reference"]["design_bed"],
