__author__ = "Jessika Nordin"
__copyright__ = "Copyright 2021, Jessika Nordin"
__email__ = "jessika.nordin@scilifelab.uu.se"
__license__ = "GPL-3"


include: "rules/common.smk"


report: "report/workflow.rst"


#include: "rules/vcf_filter.smk"


rule all:
    input:
        unpack(compile_output_list),
# can change the function in common-smk to use : unpack(compile_output_list),

module compression:
    snakefile:
        github("hydra-genetics/compression", path="workflow/Snakefile", tag=config["modules"]["compression"],)
    config:
        config

use rule crumble from compression as compression_crumble

use rule samtools_view from compression as compression_samtools_view with:
    input:
        bam="parabricks/pbrun_fq2bam/{file}.bam",
        bai="parabricks/pbrun_fq2bam/{file}.bam.bai",
        ref=config.get("reference", {}).get("fasta", ""),

use rule spring from compression as compression_spring


module cnv_sv:
    snakefile:
        github("hydra-genetics/cnv_sv", path="workflow/Snakefile", tag=config["modules"]["cnv_sv"],)
    config:
        config

use rule exomedepth from cnv_sv as cnv_exomedepth with:
    input:
        bam="cnv_sv/exomedepth_{sex}/{sample}_N.bam",
        bedfile=config.get("exomedepth", {}).get("bedfile", ""),
        ref_count=config.get("exomedepth", {}).get("ref_count", "")+"/{sex}_RefCount.mat",
    output:
        aggregated_result=temp("cnv_sv/exomedepth_{sex}/{sample}_{type}.SV.txt"),
        aed=temp("cnv_sv/exomedepth_{sex}/{sample}_{type}.aed"),
        exon=temp("cnv_sv/exomedepth_{sex}/{sample}_{type}.RData"),
        result=temp("cnv_sv/exomedepth_{sex}/{sample}_{type}.txt"),
    log:
        "cnv_sv/exomedepth_{sex}/{sample}_{type}.output.log",
    benchmark:
        repeat("cnv_sv/exomedepth_{sex}/{sample}_{type}.output.benchmark.tsv", config.get("exomedepth", {}).get("benchmark_repeats", 1))
    message:
        "{rule}: run exomedepth cnv_sv/{rule}_{wildcards.sex}/{wildcards.sample}_{wildcards.type}.input"

use rule ed_filter from cnv_sv as cnv_ed_filter with:
    input:
        exon="cnv_sv/exomedepth_{sex}/{sample}_{type}.RData",
        ED_common=config.get("exomedepth", {}).get("ED_common", ""),
        WW25=config.get("exomedepth", {}).get("WW25", ""),
        conifer=config.get("exomedepth", {}).get("conifer", ""),
    output:
        aggregated_result=temp("cnv_sv/exomedepth_{sex}/{sample}_{type}.SV.filter.txt"),
        aed=temp("cnv_sv/exomedepth_{sex}/{sample}_{type}.filter.aed"),
    log:
        "cnv_sv/exomedepth_{sex}/{sample}_{type}.filter.output.log",
    benchmark:
        repeat("cnv_sv/exomedepth_{sex}/{sample}_{type}.filter.output.benchmark.tsv", config.get("exomedepth", {}).get("benchmark_repeats", 1))
    message:
        "{rule}: filter exomedepth cnv_sv/{rule}_{wildcards.sex}/{wildcards.sample}_{wildcards.type}.RData"


module parabricks:
    snakefile:
        github("hydra-genetics/parabricks", path="workflow/Snakefile", tag=config["modules"]["parabricks"],)
    config:
        config

use rule pbrun_fq2bam from parabricks as parabricks_fq2bam

use rule pbrun_deepvariant from parabricks as parabricks_deepvariant


module prealignment:
    snakefile:
        github("hydra-genetics/prealignment", path="workflow/Snakefile", tag=config["modules"]["prealignment"],)
    config:
        config

use rule * from prealignment as prealign_*


include: "rules/add_ref_to_vcf.smk"

include: "rules/conifer.smk"


module qc:
    snakefile:
        github("hydra-genetics/qc", path="workflow/Snakefile", tag=config["modules"]["qc"],)
    config:
        config

use rule fastqc from qc as qc_fastqc

use rule mosdepth_bed from qc as qc_mosdepth_bed with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        bai="parabricks/pbrun_fq2bam/{sample}_{type}.bam.bai",
        bed=config["reference"]["design_bed"],

use rule multiqc from qc as qc_multiqc

use rule picard_collect_alignment_summary_metrics from qc as qc_pic_align_sum_met with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],

use rule picard_collect_duplication_metrics from qc as qc_pic_dup_met with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",

use rule picard_collect_gc_bias_metrics from qc as qc_pic_gc_met with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],

use rule picard_collect_hs_metrics from qc as qc_pic_hs_met with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        bait_intervals=config["reference"]["design_intervals"],
        target_intervals=config["reference"]["design_intervals"],
        reference=config["reference"]["fasta"],

use rule picard_collect_insert_size_metrics from qc as qc_pic_ins_size with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",

use rule picard_collect_multiple_metrics from qc as qc_pic_multi_met with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        ref=config["reference"]["fasta"],

use rule samtools_stats from qc as qc_samtools_stats with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",

use rule samtools_idxstats from qc as qc_samtools_idxstats with:
    input:
        bam="parabricks/pbrun_fq2bam/{sample}_{type}.bam",
        bai="parabricks/pbrun_fq2bam/{sample}_{type}.bam.bai",

module misc:
    snakefile:
        github("hydra-genetics/misc", path="workflow/Snakefile", tag=config["modules"]["misc"])
    config:
        config

use rule samtools_index from misc as misc_samtools_index with:
    input:
        cram="{file}.crumble.cram",
    output:
        crai="{file}.crumble.cram.crai",
    params:
        extra=config.get("extra", {}).get("extra", ""),
    log:
        "{file}.crumble.cram.crai.log",
    benchmark:
        repeat(
            "{file}.crumble.cram.crai.benchmark.tsv",
            config.get("samtools_index", {}).get("benchmark_repeats", 1),
        )
    message:
        "{rule}: Index {wildcards.file}.crumble.cram file"
